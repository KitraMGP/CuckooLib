/*
注意：如要更改版本请修改version.properties
 */


buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
		maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.+', changing: true
		classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id "com.github.zi_jing.cuckoogradle" version "1.0.1"
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven-publish'

Properties versionProperties = new Properties();
versionProperties.load(new StringReader(project.file('version.properties').getText('UTF-8')))

version = versionProperties.getOrDefault('modVersion', 'unknown')
group = 'com.github.zi_jing.cuckoolib'
description = 'The Cuckoo commons library.'
archivesBaseName = 'CuckooLib'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

CuckooGradle {
    mcVersion = '1.16.5'
}

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

repositories {
    if (project.hasProperty('useLocalBuildMaven') && project.property('useLocalBuildMaven') == 'true') {
        maven {
            name = 'LocalBuildMaven'
            url = 'file:///' + project.projectDir.absolutePath + '/LocalBuildMaven'
        }
    }
    if (System.getenv().containsKey("GITHUB_ACTIONS") || System.getenv().containsKey("CI")) {
        println 'Detected Github Actions environment, Aliyun Maven will be disabled'
    } else {
        maven {
            name = 'AliyunMaven'
            url = 'https://maven.aliyun.com/repository/public'
        }
    }

    jcenter()

    maven {
        // JEI
        name = 'Progwml6 maven'
        url = 'https://dvs1.progwml6.com/files/maven/'
    }

    maven {
        // TOP
        name 'tterrag maven'
        url "https://maven.tterrag.com/"
    }
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'
        expand 'version': project.version
    }
}

minecraft {
    mappings channel: 'official', version: '1.16.5'
    runs {
        client {
            workingDirectory project.file('run')
			arg "-mixin.config=" + "cuckoolib.mixins.json"
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            mods {
                cuckoolib {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
			arg "-mixin.config=" + "cuckoolib.mixins.json"
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            mods {
                cuckoolib {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'cuckoolib', '--all', '--output', file('src/generated/resources/')
            mods {
                cuckoolib {
                    source sourceSets.main
                }
            }
        }
    }
}

mixin {
    add sourceSets.main, "cuckoolib.refmap.json"
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.2.0'

    // JEI
    compileOnly fg.deobf('mezz.jei:jei-1.16.5:7.7.1.126:api')
    runtimeOnly fg.deobf('mezz.jei:jei-1.16.5:7.7.1.126')

    // TOP
    compileOnly fg.deobf('mcjty.theoneprobe:TheOneProbe-1.16:1.16-3.1.4-22:api')
    runtimeOnly fg.deobf('mcjty.theoneprobe:TheOneProbe-1.16:1.16-3.1.4-22')

    testImplementation 'junit:junit:4.12'
}

test {
    useJUnit()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

jar {
    manifest {
        attributes([
                'Specification-Title'     : 'cuckoolib',
                'Specification-Vendor'    : 'cuckoolib',
                'Specification-Version'   : '1',
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : '${version}',
                'Implementation-Vendor'   : 'cuckoolib',
                'Implementation-Timestamp': new Date().format('yyyy-MM-dd\'T\'HH:mm:ssZ'),
				"MixinConfigs": "cuckoolib.mixins.json"
        ])
    }
}

publish.dependsOn('reobfJar')

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier 'sources'
    afterEvaluate {
        finalizedBy reobfJar
    }
}

artifacts {
    archives jar
    archives sourceJar
}

publishing.publications {
    mavenJava(MavenPublication) {
        artifact jar
        artifact sourceJar
    }
}

publishing.repositories {
    if (project.hasProperty('useLocalBuildMaven') && project.property('useLocalBuildMaven') == 'true') {
        maven {
            name = 'LocalBuildMaven'
            url = 'file:///' + project.projectDir.absolutePath + '/LocalBuildMaven'
        }
    }
    maven {
        name = 'RepsyMavenForCuckooDevTeam'
        url = 'https://repo.repsy.io/mvn/zijing_233/modpublish/'
        credentials {
            if (project.hasProperty('MavenUserName') && project.hasProperty('MavenPassword')) {
                username = project.property('MavenUserName')
                password = project.property('MavenPassword')
            } else {
                println 'WARNING: There isn\'t the property "MavenUserName" or property "MavenPassword"'
                println 'Publishing to the Repsy Maven for CuckooDevTeam will be unusable.'
            }
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

if (JavaVersion.current().isJava8Compatible()) {
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}